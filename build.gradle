buildscript {
  repositories {
    jcenter()
  }
  
  dependencies {
    classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '1.4.2.RELEASE'
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'project-report'
apply plugin: 'build-dashboard'

ext {
  jacocoVersion = '0.7.7.201606060606'
  checkstyleVersion = '7.2'
  pmdVersion = '5.5.2'
  guavaVersion = '20.0'
  jadiraVersion = '5.0.0.GA'
  swaggerVersion = '2.6.1'
}

group = 'com.leanstacks'
version = '1.7.0'
sourceCompatibility = 1.8

repositories {
  jcenter()
}

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-security'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  
  compile group: 'org.springframework', name: 'spring-context-support'
  
  compile group: 'joda-time', name: 'joda-time'
  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda'
  compile group: 'com.google.guava', name: 'guava', version: guavaVersion
  compile group: 'com.github.ben-manes.caffeine', name: 'caffeine'
  compile group: 'org.jadira.usertype', name: 'usertype.extended', version: jadiraVersion
  compile group: 'io.springfox', name: 'springfox-swagger2', version: swaggerVersion
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: swaggerVersion
  
  runtime group: 'org.hsqldb', name: 'hsqldb'
  runtime group: 'mysql', name: 'mysql-connector-java'
  
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

defaultTasks 'clean', 'build'

task wrapper(type: Wrapper) {
  gradleVersion = '2.14.1'
}

task encodePassword(type: JavaExec) {
  classpath = sourceSets.test.runtimeClasspath
  main = 'com.leanstacks.ws.util.BCryptPasswordEncoderUtil'
  if(project.hasProperty('mainargs')) {
    args(mainargs.split(','))
  }
}

springBoot {
  buildInfo()
}

jacoco {
  toolVersion = jacocoVersion
}

jacocoTestReport {
  reports {
    html.enabled = true
    xml.enabled = true
    csv.enabled = false
  }
}

test.finalizedBy jacocoTestReport

checkstyle {
  toolVersion = checkstyleVersion
  config = rootProject.resources.text.fromFile('etc/checkstyle/rules.xml')
}

pmd {
  toolVersion = pmdVersion
  ruleSetConfig = rootProject.resources.text.fromFile('etc/pmd/ruleset.xml')
  ignoreFailures = true
}

check.finalizedBy projectReport

projectReport.finalizedBy buildDashboard
